buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.4.1"
        classpath "com.github.dcendents:android-maven-plugin:1.2"
    }
}

apply plugin: "com.android.library"

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'
    resourcePrefix "carousellayoutmanager__"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    configurations {
        javadocDeps
    }

    flavorDimensions "default"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:27.0.0'
    compile 'com.android.support:design:27.0.0'
    compile 'com.android.support:recyclerview-v7:27.0.0'
    javadocDeps 'com.android.support:support-annotations:27.0.0'
    javadocDeps 'com.android.support:recyclerview-v7:27.0.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError false
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

// Excluding aar artifacts from uploading
configurations.archives.artifacts.with { archives ->
    for (def iter = archives.iterator(); iter.hasNext();) {
        if (iter.next().file =~ 'aar') iter.remove();
    }
}